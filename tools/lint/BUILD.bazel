load("@bazel_skylib//rules:common_settings.bzl", "bool_flag")
load("@nogo_analyzer_bzlmod//:def.bzl", "ANALYZERS", "staticcheck_analyzers")
load("@rules_go//go:def.bzl", "TOOLS_NOGO", "nogo")

package(default_visibility = ["//:__subpackages__"])

# Flag to skip nogo analyzers (for Termux/Android compatibility)
# Usage: bazel build --//tools/lint:skip_nogo=true //...
# Or use: bazel build --config=termux //...
bool_flag(
    name = "skip_nogo",
    build_setting_default = False,
)

config_setting(
    name = "skip_nogo_enabled",
    flag_values = {":skip_nogo": "True"},
)

exports_files([
    "linters.bzl",
])

alias(
    name = "buf",
    actual = "@rules_buf_toolchains//:buf",
)

# These are the additional govet analyzers that were missing in TOOLS_NOGO.
# TODO: Add shadow analyzer once all shadow issues in codebase are fixed:
#   "@org_golang_x_tools//go/analysis/passes/shadow:go_default_library",
ADDITIONAL_GOVET = [
    "@org_golang_x_tools//go/analysis/passes/appends:go_default_library",
    "@org_golang_x_tools//go/analysis/passes/defers:go_default_library",
    "@org_golang_x_tools//go/analysis/passes/slog:go_default_library",
    "@org_golang_x_tools//go/analysis/passes/timeformat:go_default_library",
    "@org_golang_x_tools//go/analysis/passes/unusedwrite:go_default_library",
]

nogo(
    name = "platform_nogo",
    config = "nogo.json",
    visibility = ["//visibility:public"],
    # Use --//tools/lint:skip_nogo=true or --config=termux to skip analyzers
    deps = select({
        ":skip_nogo_enabled": [],
        "//conditions:default": TOOLS_NOGO + ADDITIONAL_GOVET + staticcheck_analyzers(ANALYZERS),
    }),
)
